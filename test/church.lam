# Church numerals
let id = \x. x in
let true = \a. \b. a in
let false = \a. \b. b in

let zero = \f. \x. x in
let succ = \n. \f. \x. f (n f x) in
let plus = \m. \n. \f. \x. m f (n f x) in
let mult = \m. \n. \f. m (n f) in
let exp = \m. \n. n m in
let pred = \n. \f. \x. n (\g. \h. h (g f)) (\u. x) (\u. u) in
let sub = \m. \n. (n pred) m in
let iszero = \n. n (\x. false) true in

let decode = \n. n (\x. x + 1) 0 in

let one = succ zero in
let one' = decode one in
let two = plus one one in
let two' = decode two in
let four = mult two two in
let four' = decode four in
let sixteen = mult four four in
let alot = exp two sixteen in
let alot' = decode alot in

let fact' = \f. \x. ((iszero x) (\x'. one) (\x'. mult x (f f (pred x)))) id in
let fact = fact' fact' in
let six = fact (succ two) in
let six' = decode six in

# Make it print out the results
let x = print one' in
let x = print two' in
let x = print four' in
let x = print alot' in
print six'
#\x. one' two' four' alot' six'
